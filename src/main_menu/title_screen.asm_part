JumpLoadTitleScreen:
    jp LoadTitleScreen
JumpUpdateTitleScreen:
    jp UpdateTitleScreen

LoadTitleScreen:
    di
    call LCDOff

    ; Copy tile map into VRAM.
    set_romx_bank BANK(TitleScreenTileMap)
    mem_copy TitleScreenTileMap, _SCRN0, TitleScreenTileMap.end-TitleScreenTileMap

    ld hl, JumpUpdateTitleScreen
    call SetProgramLoopCallback

    call ResetDirtyTiles

    ; Reset SCY & SCX.
    xor a
    ld [rSCY], a
    ld [rSCX], a

    ; Reset SelectedOption
    xor a
    ld [wSelectedOption], a

    ; Set Cursor Positions
    ld hl, wCursorTileIndices
    ld bc, CURSOR_START_TITLE
    ld a, b
    ld [hli], a
    ld a, c
    ld [hli], a
    ld bc, CURSOR_START_TITLE+64
    ld a, b
    ld [hli], a
    ld a, c
    ld [hli], a

    call LCDOn
    ei

    ret

UpdateTitleScreen:
    call UpdateInput

    ; Update Sound
    set_romx_bank BANK(CombatBGM)
    call _hUGE_dosound

    ; Clear Old Cursor
    call GetCursorTileIndex
    ld a, EMPTY_TILE
    call SetTile

    ; Get Input
    ld a, [wNewlyInputKeys]
    ld b, a

    ; Select Previous/Next Option
    ld a, [wSelectedOption]
.onUp
    bit PADB_UP, b
    jr z, .onDown
    dec a
    jr .updateSelectedOption
.onDown
    bit PADB_DOWN, b
    jr z, .updateSelectedOption
    inc a
.updateSelectedOption
    and a, $01 ; Title Screen has only 2 options. This allows the cursor to loop.
    ld [wSelectedOption], a

    ; Enter Selected Option
.onEnter
    bit PADB_A, b
    jr z, .end
.selectContinue
    ASSERT TITLE_OPT_CONTINUE == 0
    and a
    jr nz, .selectNewGame
    ld hl, JumpLoadGameLevel
    call SetProgramLoopCallback
    jr .end
.selectNewGame
    ASSERT TITLE_OPT_NEWGAME > 0
    ld hl, JumpLoadNewGameScreen
    call SetProgramLoopCallback

.end
    ; Set New Cursor
    call GetCursorTileIndex
    ld a, 27
    call SetTile
    call UpdateDirtyTiles

    ret