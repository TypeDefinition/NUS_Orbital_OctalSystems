LoadTitleScreen:
    di
    call LCDOff

    ; Copy tile data into VRAM.
    set_romx_bank BANK(TitleScreenTileData)
    mem_copy TitleScreenTileData, _VRAM9000, TitleScreenTileData.end-TitleScreenTileData

    ; Copy tile map into VRAM.
    set_romx_bank BANK(TitleScreenTileMap)
    mem_copy TitleScreenTileMap, _SCRN0, TitleScreenTileMap.end-TitleScreenTileMap

    ld hl, JumpUpdateTitleScreen
    call SetProgramLoopCallback

    call ResetBGWindowUpdateQueue

    ; Reset SCY & SCX.
    xor a
    ld [rSCY], a
    ld [rSCX], a

    call LCDOn
    ei

    ret

UpdateTitleScreen:
    ; Update Sound
    set_romx_bank BANK(MainMenuBGM)
    call _hUGE_dosound

    ; Get Input
    call UpdateInput
    ld a, [wNewlyInputKeys]
    ld b, a
.onA ; Stage Select
    bit PADB_A, b
    jr z, .onSelect
    xor a
    ld [wSelectedStage], a
    ld hl, JumpLoadStageSelectScreen
    call SetProgramLoopCallback
    jr .end
.onSelect ; Reset Save Files
    bit PADB_SELECT, b
    jr z, .end
    ld hl, JumpLoadResetScreen
    call SetProgramLoopCallback

.end
    ret