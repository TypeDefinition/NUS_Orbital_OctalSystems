LoadLockedStageScreen:
    di
    call LCDOff

    ld hl, JumpUpdateLockedStageScreen
    call SetProgramLoopCallback

    call ResetBGWindowUpdateQueue

    ; Reset SCY & SCX.
    xor a
    ld [rSCY], a
    ld [rSCX], a

    ; Copy tile data into VRAM.
    set_romx_bank BANK(BGWindowTileData)
    mem_copy BGWindowTileData, _VRAM9000, BGWindowTileData.end-BGWindowTileData

    IF DEF(LANGUAGE_EN)
    ; Copy font tile data into VRAM.
    set_romx_bank BANK(FontTileDataEN)
    mem_copy FontTileDataEN, _VRAM9200, FontTileDataEN.end-FontTileDataEN

    ; Copy tile map into VRAM.
    set_romx_bank BANK(LockedStageTileMapEN)
    mem_copy LockedStageTileMapEN, _SCRN0, LockedStageTileMapEN.end-LockedStageTileMapEN
    ENDC

    IF DEF(LANGUAGE_JP)
    ; Copy font tile data into VRAM.
    set_romx_bank BANK(FontTileDataJP)
    mem_copy FontTileDataJP, _VRAM9200, FontTileDataJP.end-FontTileDataJP

    ; Copy tile map into VRAM.
    set_romx_bank BANK(LockedStageTileMapJP)
    mem_copy LockedStageTileMapJP, _SCRN0, LockedStageTileMapJP.end-LockedStageTileMapJP
    ENDC

    ; Store Stage Info Address into hl.
    call GetSelectedStageParamAddr
    ; Stage Name
    ld a, [hli]
    ld [_SCRN0+UTI_STAGE_NAME], a
    ld a, [hli]
    ld [_SCRN0+UTI_STAGE_NAME+1], a

    call LCDOn
    ei
    ret

UpdateLockedStageScreen:
    ; Update Sound
    set_romx_bank BANK(MainMenuBGM)
    call _hUGE_dosound

    ; Get Input
    call UpdateInput
    ld a, [wNewlyInputKeys]
    ld b, a

.onLeft
    bit PADB_LEFT, b
    jr z, .onRight

    ld a, [wSelectedStage]
    sub a, $01
    jr nc, :+ ; If a < 0, a = 0.
    xor a
:   ld [wSelectedStage], a

    ld hl, JumpLoadStageSelectScreen
    call SetProgramLoopCallback

    jr .end

.onRight
    bit PADB_RIGHT, b
    jr z, .onB
    
    ld a, [wSelectedStage]
    inc a
    cp a, MAX_STAGES
    jr c, :+ ; If a >= MAX_STAGES, a = MAX_STAGES-1.
    ld a, (MAX_STAGES-1)
:   ld [wSelectedStage], a

    ld hl, JumpLoadStageSelectScreen
    call SetProgramLoopCallback

    jr .end

.onB
    bit PADB_B, b
    jr z, .end
    ld hl, JumpLoadTitleScreen
    call SetProgramLoopCallback

.end
    call UpdateBGWindow
    ret